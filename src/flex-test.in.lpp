/*%array*/

%option 8bit
%option batch never-interactive
%option stack
%option bison-bridge bison-locations
%option reentrant pointer
%option prefix="frma"

%option noyywrap

%option yylineno

%option debug nodefault warn

%top{
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wold-style-cast"
#pragma clang diagnostic ignored "-Wunreachable-code"
#pragma clang diagnostic ignored "-Wunused-function"
}

%{
#include <iostream>
#include <map>
#include <stdexcept>
#include <string>

#include "parserTag.hpp"
#include "lexerDriver.hpp"
#include "bison-test.hpp"

#define YYSTYPE frma::parser::semantic_type
#define YYLTYPE frma::location
#define YY_EXTRA_TYPE frma::FormaParserTag *
#define YY_DECL int frmalex(YYSTYPE *yylval_param, YYLTYPE *yylloc_param, yyscan_t yyscanner)

typedef frma::parser::token tk;

#define $x yyg->yyextra_r

#define $E(value) { $x->error(*yylloc, value); yyterminate(); }

#define $TK(value) return tk::value;

#define $SKT(text) yylval->_tok = new frma::FormaToken(text);

#define $SK $SKT(yytext)
#define $SKB $SKT($B)

#define $YKT(type, text) $SKT(text) $TK(type)
#define $YK(type) $SK $TK(type)
#define $YKB(type) $SKB $TK(type)
#define $YKE(type) $SKB $BE $TK(type)

#define $SSBEGIN(value) BEGIN(SS##value);
#define $SSPUSH(value) yy_push_state(SS##value, yyscanner);
#define $SSPOP yy_pop_state(yyscanner);

#define $END yyterminate();

#define $BS $x->bufStart();
#define $BE $x->bufEnd();
#define $BR $x->bufReturn();

#define $BW $x->buf() << yytext;

#define $BSW $BS $BW

#define $B $x->buf().str()

#define $SL yylloc->step();

#define $MN(n) yylloc->columns(n);
#define $WN(n) yylloc->lines(n);

#define $ML $MN(yyleng)
#define $MO $MN(1)

#define $WL $WN(yyleng)
#define $WO $WN(1)
YY_DECL;
%}

Whitespace    [ \r\t]
Newline       [\v\n]
LCommentOpen  @@
LCommentClose \n
LCommentInput [^\n]

BCommentOpen  @\[
BCommentClose @+\]
BCommentNoAt  [^@\n]
BCommentAts   @+[^\]\n]?

RawBlockStart @!
RawBlockOpen  \{
RawBlockClose @+\}
RawBlockNoAt  [^@\n]
RawBlockAts   @+[^\}\n]?

MetaBlockID meta

Backslash   \\

LParen      \(
RParen      \)
LBracket    \[
RBracket    \]
LBrace      \{
RBrace      \}
SQuote      \'
DQuote      \"

MKWFunction  function
MKWInterface interface
MKWLet       let
MKWOperator  operator
MKWRecord    record
MKWStruct    struct
MKWSyntax    syntax

MOPComma ,
MOPEqual =
MOPSemi  ;

IDStart    [a-zA-Z_]
PPStart    @
NumStart   [0-9]
OpStart    [`~!#$%^&*=+|;:,<.>/?-]
Identifier [0-9a-zA-Z_]
Number     [0-9a-zA-Z_.]
Operator   [`~!@#$%^&*_=+|;:,<.>/?-]
SQChar     [^'\\]
DQChar     [^"\\]
QLEscape   \\.

%x SSBComment

%x SSRawBlockStart
%x SSRawBlockID
%x SSMetaBlockID
%x SSRawBlock
%x SSMetaBlock

%%
<INITIAL,SSMetaBlock>{
  {Whitespace}+ { $SL $ML }
}

<INITIAL,SSBComment,SSMetaBlock>{
  {Newline}+    { $SL $WL }
}

<INITIAL,SSMetaBlock>{
  {LCommentOpen}{LCommentInput}*{LCommentClose} { $SL $WO }

  {BCommentOpen} { $SL $ML $SSPUSH(BComment) }
}

<SSBComment>{
  {BCommentClose}  { $SL $ML $SSPOP }
  {BCommentNoAt}+  { $SL $ML }
  {BCommentAts}+   { $SL $ML }

  <<EOF>> { $SL $ML $E("unterminated block comment") }
}

{RawBlockStart} { $SL $ML $SSPUSH(RawBlockStart) }

<SSRawBlockStart>{
  {MetaBlockID} { $SL $ML $SSBEGIN(MetaBlockID) }

  {IDStart}{Identifier}* { $SL $ML $SSBEGIN(RawBlockID) $YK(RawBlockID) }
}

<SSRawBlockID>{
  {RawBlockOpen} { $SL $ML $SSBEGIN(RawBlock) $BS }
}

<SSMetaBlockID>{
  {RawBlockOpen} { $SL $ML $SSBEGIN(MetaBlock) $TK(MetaBlockStart) }
}

<SSRawBlock>{
  {Newline}+ { $WL $BW }

  {RawBlockClose} { $ML $SSPOP $YKE(RawBlockBody) }
  {RawBlockNoAt}+ { $ML $BW }
  {RawBlockAts}+  { $ML $BW }
}

<SSMetaBlock>{
  {RawBlockClose} { $SL $ML $SSPOP $TK(MetaBlockEnd) }

  {MKWFunction}  { $SL $ML $TK(MKWFunction) }
  {MKWInterface} { $SL $ML $TK(MKWFunction) }
  {MKWLet}       { $SL $ML $TK(MKWLet) }
  {MKWOperator}  { $SL $ML $TK(MKWOperator) }
  {MKWRecord}    { $SL $ML $TK(MKWRecord) }
  {MKWStruct}    { $SL $ML $TK(MKWStruct) }
  {MKWSyntax}    { $SL $ML $TK(MKWSyntax) }

  {MOPComma} { $SL $ML $TK(MOPComma) }
  {MOPEqual} { $SL $ML $TK(MOPEqual) }
  {MOPSemi}  { $SL $ML $TK(MOPSemi) }
}

<SSRawBlock,SSMetaBlock>{
  <<EOF>> { $ML $BE $E("unterminated raw block") }
}

<INITIAL,SSMetaBlock>{
  {LParen} { $SL $ML $TK(LParen) }
  {RParen} { $SL $ML $TK(RParen) }

  {LBracket} { $SL $ML $TK(LBracket) }
  {RBracket} { $SL $ML $TK(RBracket) }

  {LBrace} { $SL $ML $TK(LBrace) }
  {RBrace} { $SL $ML $TK(RBrace) }

  {IDStart}{Identifier}* { $SL $ML $YK(Identifier) }
  {PPStart}{Identifier}+ { $SL $ML $YK(PPDirective) }
  {NumStart}{Number}*    { $SL $ML $YK(Number) }

  {SQuote}({SQChar}|{QLEscape})*{SQuote}{Identifier}* { $SL $ML $YK(SQLiteral) }
  {DQuote}({DQChar}|{QLEscape})*{DQuote}{Identifier}* { $SL $ML $YK(DQLiteral) }
}

{OpStart}{Operator}*   { $SL $ML $YK(Operator) }

<INITIAL,SSMetaBlock>{
  . { $SL $ML $E("unexpected character") }
}

<<EOF>> { $SL $ML $END }
%%

namespace frma {
  lexer::lexer(frma::FormaParserTag &tag) {
    if (int ret = frmalex_init_extra(&tag, &m_yyscanner))
      throw new std::runtime_error(std::string("Scanner creation failed with code ") + std::to_string(ret));

    tag.scan = m_yyscanner;
  }

  lexer::~lexer() {
    if (int ret = frmalex_destroy(m_yyscanner))
      throw new std::runtime_error(std::string("Scanner destructor failed with code ") + std::to_string(ret));
  }

  bool lexer::debug() const { return frmaget_debug(m_yyscanner); }

  void lexer::debug(bool value) { frmaset_debug(value, m_yyscanner); }

  FILE *lexer::inFile() const { return frmaget_in(m_yyscanner); }

  void lexer::inFile(FILE *value) { frmaset_in(value, m_yyscanner); }

  FILE *lexer::outFile() const { return frmaget_out(m_yyscanner); }

  void lexer::outFile(FILE *value) { frmaset_out(value, m_yyscanner); }
}

#pragma clang diagnostic pop