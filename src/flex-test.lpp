/*%array*/

%option 8bit
%option batch never-interactive
%option stack
%option bison-bridge bison-locations
%option reentrant pointer
%option prefix="frma"

%option noyywrap

%option yylineno

%option nodefault warn

%top{
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wold-style-cast"
#pragma clang diagnostic ignored "-Wunreachable-code"
#pragma clang diagnostic ignored "-Wunused-function"
}

%{
#include <iostream>
#include <map>
#include <stdexcept>
#include <string>

#include "parserTag.hpp"
#include "lexerDriver.hpp"
#include "bison-test.hpp"

#define YYSTYPE frma::parser::semantic_type
#define YYLTYPE frma::location
#define YY_EXTRA_TYPE frma::FormaParserTag *
#define YY_DECL int frmalex(YYSTYPE *yylval_param, YYLTYPE *yylloc_param, yyscan_t yyscanner)

typedef frma::parser::token tk;

#define $x yyg->yyextra_r

#define $E(value) { $x->error(*yylloc, value); yyterminate(); }

#define $TK(value) return tk::value;

#define $SKT(var, type, text) yylval->var = new frma::Forma##type(text);
#define $SK(var, type) $SKT(var, type, yytext)
#define $SKB(var, type) $SKT(var, type, $B)

#define $SPT(value, text) yylval->_prim = new frma::FormaPrim(frma::FormaPrim::value, text);
#define $SP(value) $SPT(value, yytext)
#define $SPB(value) $SPT(value, $B)

#define $YKT(var, type, text) $SKT(var, type, text) $TK(type)
#define $YK(var, type) $SK(var, type) $TK(type)
#define $YKB(var, type) $SKB(var, type) $TK(type)
#define $YKE(var, type) $SKB(var, type) $BE $TK(type)

#define $YPT(type, text) $SPT(type, text) $TK(type)
#define $YP(type) $SP(type) $TK(type)
#define $YPB(type) $SPB(type) $TK(type)
#define $YPE(type) $SPB(type) $BE $TK(type)

#define $MR yymore();

#define $SSPUSH(value) yy_push_state(SS##value, yyscanner);

#define $SSPOP yy_pop_state(yyscanner);

#define $END yyterminate();

#define $BS $x->bufStart();

#define $BE $x->bufEnd();

#define $BW $x->buf() << yytext;

#define $BSW $BS $BW

#define $B $x->buf().str()

#define $SL yylloc->step();

#define $MN(n) yylloc->columns(n);
#define $WN(n) yylloc->lines(n);

#define $ML $MN(yyleng)
#define $MO $MN(1)

#define $WL $WN(yyleng)
#define $WO $WN(1)
YY_DECL;
%}

Whitespace    [ \r\t]
Newline       [\v\n]
LCommentOpen  @@
LCommentClose \n
LCommentInput [^\n]

BCommentOpen  @\[
BCommentClose @+\]
BCommentNoAt  [^@\n]
BCommentAts   @+[^\]\n]?

RawBlockStart @!
RawBlockOpen  \{
RawBlockClose @+\}
RawBlockNoAt  [^@\n]
RawBlockAts   @+[^\}\n]?

Backslash   \\

LParen      \(
RParen      \)
LBracket    \[
RBracket    \]
LBrace      \{
RBrace      \}
SQuote      \'
DQuote      \"

IDStart    [a-zA-Z_]
PPStart    @
NumStart   [0-9]
OpStart    [`~!#$%^&*=+|;:,<.>/?-]
Identifier [0-9a-zA-Z_]
Number     [0-9a-zA-Z_.]
Operator   [`~!@#$%^&*_=+|;:,<.>/?-]
SQChar     [^'\\]
DQChar     [^"\\]
QLEscape   \\.

%x SSBComment
%x SSRawBlock

%%
{Whitespace}+ { $SL $ML }
{Newline}+    { $SL $WL }

{LCommentOpen}{LCommentInput}*{LCommentClose} { $SL $WO }

{BCommentOpen} { $SL $ML $SSPUSH(BComment) }

<SSBComment>{
  {Newline}+ { $SL $WL }

  {BCommentClose}  { $SL $ML $SSPOP }
  {BCommentNoAt}+  { $SL $ML }
  {BCommentAts}+   { $SL $ML }

  <<EOF>> { $SL $ML $E("unterminated block comment") }
}

{RawBlockStart}{IDStart}{Identifier}*{RawBlockOpen} { $SL $ML $SSPUSH(RawBlock) $BSW }

<SSRawBlock>{
  {Newline}+ { $WL $BW }

  {RawBlockClose} { $ML $BW $SSPOP $YPE(RawBlock) }
  {RawBlockNoAt}+ { $ML $BW }
  {RawBlockAts}+  { $ML $BW }

  <<EOF>> { $ML $BE $E("unterminated raw block") }
}

{LParen} { $SL $ML $TK(LParen) }
{RParen} { $SL $ML $TK(RParen) }

{LBracket} { $SL $ML $TK(LBracket) }
{RBracket} { $SL $ML $TK(RBracket) }

{LBrace} { $SL $ML $TK(LBrace) }
{RBrace} { $SL $ML $TK(RBrace) }

{IDStart}{Identifier}* { $SL $ML $YP(Identifier) }
{PPStart}{Identifier}+ { $SL $ML $YP(PPDirective) }
{NumStart}{Number}*    { $SL $ML $YP(Number) }
{OpStart}{Operator}*   { $SL $ML $YP(Operator) }

{SQuote}({SQChar}|{QLEscape})*{SQuote}{Identifier}* { $SL $ML $YP(SQLiteral) }
{DQuote}({DQChar}|{QLEscape})*{DQuote}{Identifier}* { $SL $ML $YP(DQLiteral) }

. { $SL $ML $E("unexpected character") }

<<EOF>> { $SL $ML $END }
%%

namespace frma {
  lexer::lexer(frma::FormaParserTag &tag) {
    if (int ret = frmalex_init_extra(&tag, &m_yyscanner))
      throw new std::runtime_error(std::string("Scanner creation failed with code ") + std::to_string(ret));

    tag.scan = m_yyscanner;
  }

  lexer::~lexer() {
    if (int ret = frmalex_destroy(m_yyscanner))
      throw new std::runtime_error(std::string("Scanner destructor failed with code ") + std::to_string(ret));
  }

  bool lexer::debug() const { return frmaget_debug(m_yyscanner); }

  void lexer::debug(bool value) { frmaset_debug(value, m_yyscanner); }

  FILE *lexer::inFile() const { return frmaget_in(m_yyscanner); }

  void lexer::inFile(FILE *value) { frmaset_in(value, m_yyscanner); }

  FILE *lexer::outFile() const { return frmaget_out(m_yyscanner); }

  void lexer::outFile(FILE *value) { frmaset_out(value, m_yyscanner); }
}

#pragma clang diagnostic pop